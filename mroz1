 /* mroz1.do: Stata program to estimate binary choice models using Mroz's data
*/

clear
#delimit ;
infile lfp whrs kl6 k618 wa we ww rpwg hhrs ha he hw faminc mtr 
       wmed wfed un cit ax
     using e:\E8760\data-fl06\mroz.raw in 1/753;
log using e:\E8760\data-fl06\mroz1.log,replace;

* Descriptive statistics for the data; 
summarize;

/* Create variables based on polynomial transformation of age, 
  education and experience. prin denotes property income  */
gen wa2=wa*wa;
gen we2=we*we;
gen ax2=ax*ax;
gen wa3=wa2*wa;
gen we3 =we2*we;
gen wawe=wa*we;
gen prin = (faminc - (ww *whrs))/1000;

/* 
Use  global macro to create namelist.  For details on global and local macros, 
      please  see Stata 9 User's Guide, Chapter/section 18.3.2. 
*/
   global xwopolyt  "kl6 k618 wa we  wfed wmed un cit prin ax";
    global xall  " kl6 k618 wa we wa2 we2 wawe wa3 we3 wfed wmed un cit prin ax ax2";

*  Run LPM regression(OLS) on lfp;
  reg lfp $xwopolyt;
  reg lfp $xall;

* Run binary logit MLE on lfp;
  logit lfp $xall;

* Run binary probit  MLE on lfp;
  probit lfp $xall;

* Run LPM, logit probit with robust standard errors;
   reg lfp $xall,robust;    
   logit lfp $xall,robust;
   probit lfp $xall,robust;
    test we we2 we3; 

/* 
Get marginal effects and elasticities from the LPM, logit and probit regressions.  
 If there is a dummy variable in X, mfx will calculate the discrete change as the dummy variable 
changes from 0 to 1. Please see the post-estimation command mfx for options and other details.
*/ 

  reg lfp $xall,robust;
    mfx compute;
	mfx  compute, eyex;
  logit lfp $xall,robust;
    mfx compute; 
    mfx compute,eyex;
   probit lfp $xall,robust;
      mfx compute;
	mfx compute,eyex;

log close;
